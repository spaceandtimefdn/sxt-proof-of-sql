// SPDX-License-Identifier: UNLICENSED
// This is licensed under the Cryptographic Open Software License 1.0
pragma solidity ^0.8.28;

import "../base/Constants.sol";
import "../base/Errors.sol";
import {VerificationBuilder} from "../builder/VerificationBuilder.presl";

/// @title FilterExec
/// @dev Library for handling filter execution plans
library FilterExec {
    /// @notice Evaluates a filter execution plan
    /// @custom:as-yul-wrapper
    /// #### Wrapped Yul Function
    /// ##### Signature
    /// ```yul
    /// function filter_exec_evaluate(plan_ptr, builder_ptr) -> plan_ptr_out, evaluations_ptr, output_length, output_chi_eval
    /// ```
    /// ##### Parameters
    /// * `plan_ptr` - calldata pointer to the filter execution plan
    /// * `builder_ptr` - memory pointer to the verification builder
    /// ##### Return Values
    /// * `plan_ptr_out` - pointer to the remaining plan after consuming the filter execution plan
    /// * `evaluations_ptr` - pointer to the evaluations
    /// * `output_length` - the length of the column of ones
    /// * `output_chi_eval` - pointer to the evaluation of a column of 1s with same length as output
    /// @notice Evaluates two sub-expressions and produces identity constraints checking their equality
    /// @notice ##### Constraints
    /// * Inputs: \\(S=\texttt{selection}\\), \\(C_1,\ldots,C_\ell=\texttt{c}\\) with length \\(n\\), and thus \\(\chi_{[0,n)}=\texttt{input_chi_eval}\\).
    ///   Note: `proof_expr_evaluate` guarantees that the lengths of these inputs equals the lengths of \\(chi_{[0,n)}\\).
    ///         It also guarantees that the inputs have the correct evaluations.
    ///         So, because we are assuming the query is valid, we know that \\(S\\) must be a column of boolean values.
    /// * Outputs: \\(D_1,\ldots,D_\ell=\texttt{d}\\) with length \\(m\\), and thus \\(\chi_{[0,m)}=\texttt{output_chi_eval}\\).
    /// * Hints: \\(C^\star=\texttt{c_star}\\) and \\(D^\star=\texttt{d_star}\\)
    /// * Challenges: \\(\alpha=\texttt{alpha}\\), \\(\beta=\texttt{beta}\\)
    /// * Helpers: \\(\bar{C} \=\texttt{c_fold} :\equiv \sum_{i=1}^{\ell} C_i \beta^{\ell-i}\\) and \\(\bar{D} \=\texttt{d_fold} :\equiv \sum_{i=1}^{\ell} D_i \beta^{\ell-i}\\)
    /// * Constraints:
    /// \\[\begin{aligned}
    /// C^\star \cdot S - D^\star &\overset{\sum}{=} 0\\\\
    /// (1 + \alpha\cdot \bar{C})\cdot C^\star - \chi_{[0,n)} &\equiv 0\\\\
    /// (1 + \alpha\cdot \bar{D})\cdot D^\star - \chi_{[0,m)} &\equiv 0\\\\
    /// \alpha\cdot\bar{D}\cdot(\chi_{[0,m)}-1) &\equiv 0\\\\
    /// \end{aligned}\\]
    /// Note: the notation \\(A\overset{\sum}{=}B\\) is used to indicate the zero-sum constratin.
    /// That is, that the sum of the elements of \\(A\\) equals the sum of the elements in \\(B\\).
    /// @notice To satisfy these constraints, we have that \\[\begin{aligned}
    /// C^\star[i] &= \begin{cases} \frac{1}{1+\alpha\cdot \bar{C}[i]} & \text{ when } i < n\\\\ 0 & \text{else}\end{cases}\\\\
    /// D^\star[i] &= \begin{cases} \frac{1}{1+\alpha\cdot \bar{D}[i]} & \text{ when } i < m\\\\ 0 & \text{else}\end{cases}
    /// \end{aligned}\\]
    /// @notice ##### Proof of Correctness
    /// @notice **Theorem:** Consider columns \\(C_1,\ldots,C_\ell\\) and \\(S\\) of length \\(n\\), where \\(S[i]=0\text{ or }1\\) for all \\(i\\). Given columns \\(D_1,\ldots,D_\ell\\),
    /// we have that
    /// \\[\begin{aligned}
    /// \\{(D_1[i],\ldots,D_\ell[i])\mid i<m\\} &= \\{(C_1[i],\ldots,C_\ell[i])\mid S[i]=1\\}\\\\
    /// \end{aligned}\\]
    /// if and only if
    /// \\[\sum_{i<n}\frac{S[i]}{1+\alpha\left(\beta^{\ell-1}C_0[i]+\cdots+C_\ell[i]\right)}=\sum_{i<m}\frac{1}{1+\alpha\left(\beta^{\ell-1}D_0[i]+\cdots+D_\ell[i]\right)}\\]
    /// @notice **Completeness Proof:**
    /// TODO
    /// @notice **Soundness Proof:**
    /// TODO
    /// ##### Proof Plan Encoding
    /// The filter plan is encoded as follows:
    /// 1. The input proof plan
    /// 2. The selection/filtering condition expression
    /// 3. The number of input/output columns (64 bit integer)
    /// 4. The input column expressions, in order
    /// @dev Evaluates a filter execution plan by checking the filter condition on each row
    /// @param __plan The filter execution plan data
    /// @param __builder The verification builder
    /// @return __planOut The remaining plan after processing
    /// @return __builderOut The verification builder result
    /// @return __evaluationsPtr The evaluations pointer
    /// @return __outputLength The length of the output chi evaluation
    /// @return __outputChiEvaluation The output chi evaluation
    function __filterExecEvaluate( // solhint-disable-line gas-calldata-parameters
    bytes calldata __plan, VerificationBuilder.Builder memory __builder)
        external
        pure
        returns (
            bytes calldata __planOut,
            VerificationBuilder.Builder memory __builderOut,
            uint256[] memory __evaluationsPtr,
            uint256 __outputLength,
            uint256 __outputChiEvaluation
        )
    {
        uint256[] memory __evaluations;
        assembly {
            // import mulmod_bn254 from ../base/MathUtil.presl
            // import builder_consume_challenge, builder_consume_chi_evaluation_with_length, builder_get_column_evaluations from ../builder/VerificationBuilder.presl
            // import proof_expr_evaluate from ../proof_exprs/ProofExpr.presl
            // import verify_filter from ../proof_gadgets/FilterBase.presl
            // import proof_plan_evaluate from ProofPlan.presl

            function compute_filter_folds(plan_ptr, builder_ptr, input_chi_eval, beta) -> plan_ptr_out, c_fold, d_fold, evaluations_ptr {
                let column_count := shr(UINT64_PADDING_BITS, calldataload(plan_ptr))
                plan_ptr := add(plan_ptr, UINT64_SIZE)

                plan_ptr, c_fold := fold_expr_evals(plan_ptr, builder_ptr, input_chi_eval, beta, column_count, builder_get_column_evaluations(builder_ptr))
                d_fold, evaluations_ptr := fold_first_round_mles(builder_ptr, beta, column_count)
                plan_ptr_out := plan_ptr
            }

            function filter_exec_evaluate(plan_ptr, builder_ptr) -> plan_ptr_out, evaluations_ptr, output_length, output_chi_eval {
                let input_chi_eval, selection_eval, c_fold, d_fold
                {
                    let alpha := builder_consume_challenge(builder_ptr)
                    let beta := builder_consume_challenge(builder_ptr)
                    {
                        let input_length, input_evaluations_ptr
                        plan_ptr, input_evaluations_ptr, input_length, input_chi_eval :=
                            proof_plan_evaluate(plan_ptr, builder_ptr)
                    }

                    plan_ptr, selection_eval := proof_expr_evaluate(plan_ptr, builder_ptr, input_chi_eval, builder_get_column_evaluations(builder_ptr))

                    plan_ptr, c_fold, d_fold, evaluations_ptr :=
                         compute_filter_folds(plan_ptr, builder_ptr, input_chi_eval, beta)
                    c_fold := mulmod_bn254(alpha, c_fold)
                    d_fold := mulmod_bn254(alpha, d_fold)
                }
                output_length, output_chi_eval := builder_consume_chi_evaluation_with_length(builder_ptr)

                verify_filter(builder_ptr, c_fold, d_fold, input_chi_eval, output_chi_eval, selection_eval)

                plan_ptr_out := plan_ptr
            }

            let __planOutOffset
            __planOutOffset, __evaluations, __outputLength, __outputChiEvaluation :=
                filter_exec_evaluate(__plan.offset, __builder)
            __planOut.offset := __planOutOffset
            // slither-disable-next-line write-after-write
            __planOut.length := sub(__plan.length, sub(__planOutOffset, __plan.offset))
        }
        __evaluationsPtr = __evaluations;
        __builderOut = __builder;
    }
}
