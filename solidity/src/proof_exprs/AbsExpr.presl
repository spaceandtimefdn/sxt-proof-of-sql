// SPDX-License-Identifier: UNLICENSED
// This is licensed under the Cryptographic Open Software License 1.0
pragma solidity ^0.8.26;

import "../base/Constants.sol";
import "../base/Errors.sol";
import {VerificationBuilder} from "../builder/VerificationBuilder.presl";

/// @title AbsExpr
/// @dev Library for handling absolute values
library AbsExpr {
    /// @notice Evaluates an abs expression
    /// @custom:as-yul-wrapper
    /// #### Wrapped Yul Function
    /// ##### Signature
    /// ```yul
    /// abs_expr_evaluate(expr_ptr, builder_ptr, chi_eval, accessor) -> expr_ptr_out, eval
    /// ```
    /// ##### Parameters
    /// * `expr_ptr` - calldata pointer to the expression data
    /// * `builder_ptr` - memory pointer to the verification builder
    /// * `chi_eval` - the chi value for evaluation
    /// * `accessor` - memory pointer to the collection of evals
    /// ##### Return Values
    /// * `expr_ptr_out` - pointer to the remaining expression after consuming both sub-expressions
    /// * `eval` - the evaluation result from the builder's final round MLE
    /// @notice Evaluates an expression and returns its absolute value
    /// @param __expr The abs expression data
    /// @param __builder The verification builder
    /// @param __chiEval The chi value for evaluation
    /// @param __accessor The collection of evals
    /// @return __exprOut The remaining expression after processing
    /// @return __builderOut The verification builder result
    /// @return __eval The evaluated result
    function __absExprEvaluate( // solhint-disable-line gas-calldata-parameters
    bytes calldata __expr, VerificationBuilder.Builder memory __builder, uint256 __chiEval, uint256[] memory __accessor)
        external
        pure
        returns (bytes calldata __exprOut, VerificationBuilder.Builder memory __builderOut, uint256 __eval)
    {
        assembly {
            // import addmod_bn254, submod_bn254, mulmod_bn254 from ../base/MathUtil.presl
            // import proof_expr_evaluate from ProofExpr.presl
            // import sign_expr_evaluate from ../proof_gadgets/SignExpr.presl
            // import builder_consume_final_round_mle, builder_produce_identity_constraint from ../builder/VerificationBuilder.presl

            function abs_expr_evaluate(expr_ptr, builder_ptr, chi_eval, accessor) -> expr_ptr_out, result_eval {
                let input_eval
                expr_ptr, input_eval := proof_expr_evaluate(expr_ptr, builder_ptr, chi_eval, accessor)

                let is_negative_eval := sign_expr_evaluate(input_eval, builder_ptr, chi_eval)

                result_eval := builder_consume_final_round_mle(builder_ptr)

                builder_produce_identity_constraint(
                    builder_ptr, addmod_bn254(result_eval, mulmod_bn254(input_eval, submod_bn254(mulmod_bn254(2, is_negative_eval), 1))), 2
                )

                expr_ptr_out := expr_ptr
            }

            let __exprOutOffset
            __exprOutOffset, __eval := abs_expr_evaluate(__expr.offset, __builder, __chiEval, __accessor)
            __exprOut.offset := __exprOutOffset
            // slither-disable-next-line write-after-write
            __exprOut.length := sub(__expr.length, sub(__exprOutOffset, __expr.offset))
        }
        __builderOut = __builder;
    }
}
